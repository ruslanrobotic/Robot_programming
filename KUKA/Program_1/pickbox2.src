&ACCESS RVO1
&COMMENT 
&ACCESS RVO
&PARAM EDITMASK = *
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM DISKPATH = KRC:\R1\Program\program_1
DEF pickbox2(X_box:in)
DECL POS X_box

XP1.x = XP1m.x + 3 * (395 - X_box)
XP2.x = XP1.x
XP3.x = XP1.x
XP4.x = XP1.x
XP5.x = XP1.x

;FOLD PTP HOME  Vel=100 % DEFAULT Tool[2]:tool_empty Base[0];%{PE}%R 8.3.22,%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:100, 7:DEFAULT
$BWDSTART=FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS(#PTP_PARAMS,100)
PTP XHOME 
;ENDFOLD

IF (Air_Supply_OK == FALSE) THEN
    HALT
    $MSG_T.TYP = #quit
    $MSG_T.KEY[]="Davleniye vozdukha ne v norme" ;MSG
    $MSG_T.VALID=TRUE ;vivvod MSG
    ; wait for aknowledgement
    WHILE  $MSG_T.VALID == true 
    ; WAIT SEC 0.05
    ENDWHILE 
ENDIF

; approach
;FOLD LIN P1 CONT Vel=2.00000 m/s CPDAT1 Tool[2]:tool_empty Base[3]:pickbox1;%{PE}%R 8.3.22,%MKUKATPBASIS,%CMOVE,%VLIN,%P 1:LIN, 2:P1, 3:C_DIS C_DIS, 5:2.00000, 7:CPDAT1
$BWDSTART=FALSE
LDAT_ACT=LCPDAT1
FDAT_ACT=FP1
BAS(#CP_PARAMS,2.00000)
LIN XP1 C_DIS C_DIS
;ENDFOLD

; descent to wall
;FOLD LIN P2 CONT Vel=2.00000 m/s CPDAT2 Tool[2]:tool_empty Base[3]:pickbox1;%{PE}%R 8.3.22,%MKUKATPBASIS,%CMOVE,%VLIN,%P 1:LIN, 2:P2, 3:C_DIS C_DIS, 5:2.00000, 7:CPDAT2
$BWDSTART=FALSE
LDAT_ACT=LCPDAT2
FDAT_ACT=FP2
BAS(#CP_PARAMS,2.00000)
LIN XP2 C_DIS C_DIS
;ENDFOLD

;moving boxes from the wall
;FOLD LIN P3 CONT Vel=2.00000 m/s CPDAT3 Tool[3]:tool_2_box Base[3]:pickbox1;%{PE}%R 8.3.22,%MKUKATPBASIS,%CMOVE,%VLIN,%P 1:LIN, 2:P3, 3:C_DIS C_DIS, 5:2.00000, 7:CPDAT3
$BWDSTART=FALSE
LDAT_ACT=LCPDAT3
FDAT_ACT=FP3
BAS(#CP_PARAMS,2.00000)
LIN XP3 C_DIS C_DIS
;ENDFOLD

; descent
;FOLD LIN P4 CONT Vel=2.00000 m/s CPDAT4 Tool[2]:tool_empty Base[3]:pickbox1;%{PE}%R 8.3.22,%MKUKATPBASIS,%CMOVE,%VLIN,%P 1:LIN, 2:P4, 3:C_DIS C_DIS, 5:2.00000, 7:CPDAT4
$BWDSTART=FALSE
LDAT_ACT=LCPDAT4
FDAT_ACT=FP4
BAS(#CP_PARAMS,2.00000)
LIN XP4 C_DIS C_DIS
;ENDFOLD

; pick up boxes
;FOLD OUT 1 'finger right'  State= TRUE;%{PE}%R 8.3.22,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:1, 3:finger right, 5:TRUE, 6:

$OUT[1]=TRUE
;ENDFOLD
;FOLD OUT 3 'finger left'  State= TRUE;%{PE}%R 8.3.22,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:3, 3:finger left, 5:TRUE, 6:

$OUT[3]=TRUE
;ENDFOLD
;FOLD WAIT Time= 0.5 sec;%{PE}%R 8.3.22,%MKUKATPBASIS,%CWAIT,%VWAIT,%P 2:0.5
WAIT SEC 0.5
;ENDFOLD

; pulling out the support
;FOLD OUT 2 'support hook right' State=TRUE ;%{PE}%R 8.3.32,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:2, 3:support hook right, 5:TRUE, 6:
$OUT[2]=TRUE
;ENDFOLD
;FOLD OUT 4 'support hook left' State=TRUE ;%{PE}%R 8.3.32,%MKUKATPBASIS,%COUT,%VOUTX,%P 2:4, 3:support hook left, 5:TRUE, 6:
$OUT[4]=TRUE
;ENDFOLD
;FOLD WAIT Time=1 sec;%{PE}%R 8.3.32,%MKUKATPBASIS,%CWAIT,%VWAIT,%P 3:1
WAIT SEC 1
;ENDFOLD

L1_Product_Picked = TRUE

; taking boxes out
;FOLD LIN P5  Vel=2.00000 m/s CPDAT5 Tool[3]:tool_2_box Base[3]:pickbox1;%{PE}%R 8.3.22,%MKUKATPBASIS,%CMOVE,%VLIN,%P 1:LIN, 2:P5, 3:, 5:2.00000, 7:CPDAT5
$BWDSTART=FALSE
LDAT_ACT=LCPDAT5
FDAT_ACT=FP5
BAS(#CP_PARAMS,2.00000)
LIN XP5 
;ENDFOLD

;check for allowance to leave the pick point zone
IF (L1_Pick_Point_Clear == FALSE) THEN
    $MSG_T.TYP = #quit
    $MSG_T.KEY[]="Net razresheniya pokinut' zonu vzyatiya korobok" ;MSG
    $MSG_T.VALID=TRUE ;vivvod MSG
    ; wait for aknowledgement
    WHILE  $MSG_T.VALID == true 
    ; WAIT SEC 0.05
    ENDWHILE 
ENDIF
;FOLD WAIT FOR ( L1_Pick_Point_Clear  );%{PE}%R 8.3.22,%MKUKATPBASIS,%CEXT_WAIT_FOR,%VEXT_WAIT_FOR,%P 2:, 4:, 5:L1_Pick_Point_Clear, 6:1, 7:, 9:
WAIT FOR  (  (L1_Pick_Point_Clear) ) 
;ENDFOLD

L1_Product_Picked = FALSE ; reset the signal

; depature
;FOLD LIN P6 CONT Vel=2.00000 m/s CPDAT1 Tool[3]:tool_2_box Base[3]:pickbox1;%{PE}%R 8.3.22,%MKUKATPBASIS,%CMOVE,%VLIN,%P 1:LIN, 2:P6, 3:C_DIS C_DIS, 5:2.00000, 7:CPDAT1
$BWDSTART=FALSE
LDAT_ACT=LCPDAT6
FDAT_ACT=FP6
BAS(#CP_PARAMS,2.00000)
LIN XP6 C_DIS C_DIS
;ENDFOLD

; movement to home
;FOLD PTP HOME  Vel= 100 % DEFAULT;%{PE}%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:100, 7:DEFAULT
$BWDSTART = FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS (#PTP_PARAMS,100 )
$H_POS=XHOME
PTP  XHOME
;ENDFOLD

END